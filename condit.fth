\ Stack-preserving THROW/CATCH in terms of ANS EXCEPTION words

VARIABLE CATCHDEPTH   VARIABLE STASHDEPTH
CREATE STASH   32 CELLS ALLOT

: CATCH ( ... xt -- ... 0 | ... )
  CATCHDEPTH @ >R   DEPTH 1- CATCHDEPTH !   CATCH IF
    STASHDEPTH @   DUP >R CELLS STASH + BEGIN   R@ 0 > WHILE
    1 CELLS - DUP @ SWAP   R> 1- >R REPEAT DROP   BEGIN
    R@ 0 < WHILE   DROP   R> 1+ >R REPEAT RDROP
  ELSE 0   THEN R> CATCHDEPTH ! ;

: THROW ( ... -* )   DEPTH CATCHDEPTH @ - DUP STASHDEPTH !
  >R STASH BEGIN   R@ 0 > WHILE   TUCK ! CELL+   R> 1- >R REPEAT
  RDROP   1 THROW ;

: TEST-NOTHROW   ;
: TEST-NOCATCH   ['] TEST-NOTHROW CATCH ;
\ result: 0

: TEST-UNDERTHROW   DROP THROW ;
: TEST-UNDERCATCH   1 2 3   ['] TEST-UNDERTHROW CATCH ;
\ result: 1 2

: TEST-OVERTHROW   4 5 THROW ;
: TEST-OVERCATCH   3   ['] TEST-OVERTHROW CATCH ;
\ result: 3 4 5

: TEST-NESTTHROW   TEST-NOCATCH TEST-UNDERCATCH
  TEST-OVERCATCH THROW ;
: TEST-NESTCATCH   -2 -1 ['] TEST-NESTTHROW CATCH ;
\ result: -2 -1 0 1 2 3 4 5
